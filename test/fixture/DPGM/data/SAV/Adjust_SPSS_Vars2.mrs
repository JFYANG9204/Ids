'*****FILE VERSION=1, DATE LAST MODIFIED=2011/06/13, KO

'This script reads in the Excel file produced in Adjust_SPSS_Vars1.mrs file and 
'creates an SPSS Syntax file (.sps) that can be run against the SAV file 
'to rename and remove variables, and to adjust the labels for each variable.

#include "..\..\job.ini"
'**************************************************************************************
'**************************************************************************************
'**************************************************************************************

'CHANGE AS NEEDED:  path and name to output Excel file generated from Adjust_SPSS_Vars1.mrs file
#define XL_FILE JOB_ROOT + "data\sav\Output\Adjust_SPSS_Vars.xls"

'CHANGE AS NEEDED:  name of Excel worksheet 
#define XL_SHEET_NAME "Name_Variables"

'CHANGE AS NEEDED:  path and name of output SPSS syntax file 
#define SYNTAX_FILE JOB_ROOT + "data\sav\Output\Adjust_SPSS_Vars.sps"


'**************************************************************************************
'**************************************************************************************
'**************************************************************************************


'**************************************************************************************
'No Need To Edit Below This Line
'**************************************************************************************



Dim xlApp
Dim xlWorkbook
Dim xlWorksheet

dim xlCol
dim xlRow

dim Syntax
dim fso
dim strKeep
dim strLabel

set fso = CreateObject("scripting.filesystemobject")
Set xlApp = CreateObject("Excel.Application")                           
'xlApp.Visible = true 
Set xlWorkbook = xlApp.Workbooks.open(XL_FILE) 
Set xlWorksheet = xlWorkbook.Worksheets[XL_SHEET_NAME] 
'debug.Log(SYNTAX_FILE)
set Syntax = fso.CreateTextFile(SYNTAX_FILE,True,False)


'create the rename variable statements
for xlRow = 4 to clong(xlWorksheet.Cells.SpecialCells(11).Row) 'scan down the rows that contain data

        if xlWorksheet.cells[xlRow][2] <> "" then
                syntax.WriteLine("RENAME VARIABLES (" + xlWorksheet.cells[xlRow][1] + " = " + xlWorksheet.cells[xlRow][2] + ").")
    end if
        'create the list of variable to delete 
        if xlWorksheet.cells[xlRow][5] <> "" then
                strKeep = strKeep + iif(xlWorksheet.cells[xlRow][2] <> "",xlWorksheet.cells[xlRow][2],xlWorksheet.cells[xlRow][1]) + ","
        end if
next

strKeep = left(strKeep,len(strKeep) -1)


'create the VARIABLE Labels statements
syntax.WriteLine("VARIABLE Labels")
for xlRow = 4 to clong(xlWorksheet.Cells.SpecialCells(11).Row) 'scan down the rows that contain data
   if xlWorksheet.cells[xlRow][4] <> "" then
        strLabel = replace(xlWorksheet.cells[xlRow][4],"'","''")
        syntax.WriteLine(iif(xlWorksheet.cells[xlRow][2] <> "",xlWorksheet.cells[xlRow][2],xlWorksheet.cells[xlRow][1]) + " '" + strLabel + "'")
   end if
next
syntax.WriteLine(".")

syntax.WriteLine("DELETE VARIABLES " + strKeep + ".") 

syntax.WriteLine("SAVE OUTFILE='" + xlWorkSheet.cells[2][2] + "'.")
syntax.WriteLine("EXECUTE.")

set fso = null
xlApp.workbooks.close()
